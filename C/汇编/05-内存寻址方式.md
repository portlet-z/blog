## 处理字符问题

### 字符

- 汇编程序中，用'......'的方式指明数据是以字符的形式给出的，编译器将把它们转化成相应的ASCII码

```assembly
assume cs:code, ds:data
data segment
	db 'unIX'
	db 'foRK'
data ends
code segment
start: mov al, 'a'
			 mov bl, 'b'
			 mov ax, 4C00H
			 int 21H
code ends
end start
```

### 大小写转换

| 大写 | 二进制   | 小写 | 二进制   |
| ---- | -------- | ---- | -------- |
| A    | 01000001 | a    | 01100001 |
| B    | 01000010 | b    | 01100010 |
| C    | 01000011 | c    | 01100011 |
| D    | 01000100 | d    | 01100100 |

- 大写+20H --> 小写； 小写-20H --> 大写
- 要求：对第一个字符串转大写，对第二个字符串转小写
- 逻辑与指令： and dest, src
- 逻辑或指令：or dest, src

```assembly
assume cs:code, ds:data
data segment
	db 'unIX'
	db 'foRK'
data ends
code segment
start: mov ax, data
	     mov ds, ax
	     ; 小写转大写
	     mov bx, 0
	     mov cx, 4
	  s: mov al, [bx]
	  	 and al, 11011111B
	  	 mov [bx], al
	  	 inc bx
	  	 loop s
	  	 
	  	 ; 大写转小写
	     mov bx, 4
	 	   mov cx, 4
	 s0: mov al, [bx]
	 	   or al, 00100000B
	 	   mov [bx], al
	 	   inc bx
	 	   loop s0

			 mov ax, 4C00H
			 int 21H
code ends
end start
```

## [bx+idata]方式寻址

### [bx+idata]的含义

- [bx_idata]表示一个内存单元，它的偏移地址为(bx) + idata (bx中的数值加上idata)
- mov ax, [bx + 200]    mov ax, [200 + bx]的含义
  - 将一个内存单元的内容送入ax
  - 这个内存单元的长度为2字节（字单元），存放一个字
  - 内存单元的段地址再ds中，偏移地址为200加上bx中的数值
  - 数学化的描述为：(ax) = ((ds) * 16 + 200 + (bx))
- 指令mov ax, [bx + 200]的其他写法
  - mov ax, [200 + bx]
  - mov ax, 200[bx]
  - mov ax, [bx].200

### 应用：用[bx+idata]的方式进行数组的处理

- 大小写转换

```assembly
assume cs:code, ds:data
data segment
	db 'unIX'
	db 'foRK'
data ends
code segment
start: mov ax, data
			 mov ds, ax
			 mov bx, 0
			 mov cx, 4
		s: mov al, [bx]
			 and al, 11011111B
			 mov [bx], al
			 mov al, [bx+4]
			 or al, 00100000B
			 mov [bx+4], al
			 inc bx
			 loop s
			 
			 mov ax, 4C00H
			 int 21H
code ends
end
```

- [bx+idata]的方式为高级语言实现数组提供了便利机制

```c
char a[4] = 'unIX';
char b[4] = 'foRK';
int main() {
  int i = 0;
  do {
    a[i] = a[i] & 0xDF;
    b[i] = b[i] | 0x20;
    i++;
  } while (i < 4);
}
```

## SI和DI寄存器

### SI和DI常执行与地址有关的操作

- SI和DI是8086CPU中和BX功能相近的寄存器
  - 区别：SI和DI不能够分成两个8位寄存器来使用
  - BX: 通用寄存器，在计算存储器地址时，常作为基址寄存器用
  - SI: source index, 源变址寄存器
  - DI: destination index, 目标变址寄存器
- 下面三组指令实现了相同的功能

```assembly
mov bx, 0
mov ax, [bx]

mov si, 0
mov ax, [si]

mov di, 0
mov ax, [di]
```

```assembly
mov bx, 0
mov ax, [bx+123]

mov si, 0
mov ax, [si+123]

mov di, 0
mov ax, [di+123]
```

### 应用SI和DI

- 问题：用寄存器SI和DI实现字符串'welcome to masm!'复制到它后面的数据区中
- 解决方案：
  - 源数据起始地址：datasg: 0
  - 目标数据起始地址：datasg: 16
  - 用ds:si指向要复制的原始字符串
  - 用ds:di指向目的空间
  - 然后用一个循环来完成复制

```assembly
assume cs:codesg, ds:datasg
datasg segment
	db 'welcome to masm!'
	db '................'
datasg ends
codesg segment
start: mov ax, datasg
			 mov ds, ax
			 mov si, 0
			 mov di, 16
			 mov cx, 8
		s: mov ax, [si]
		   mov [di], ax
		   add si, 2
		   add di, 2
		   loop s
			 
			 mov ax, 4C00H
			 int 21H
codesg ends
end start
```

## [bx+si]和[bx+di]方式寻址

### [bx+si]和[bx+di]方式指定地址

- [bx+si]表示一个内存单元：偏移地址为(bx) + (si) (即bx中的数值加上si中的数值)
- 指令mov ax, [bx+si]的含义
  - 将一个内存单元的内容送入ax
  - 这个内存单元的长度为2字节（字单元），存放一个字
  - 偏移地址为bx中的数值加上si中的数值
  - 段地址再ds中
- 指令mov ax, [bx+si]的数学化的描述为 (ax) = ((ds) * 16 + (bx) + (si))
- mov ax, [bx+si]的其他写法：`mov ax, [bx][si]`

### 应用案例

- 内存中数据 2000:1000 BE 00 06 00 00 00
- 分析程序执行后，ax, bx, cx中的内容

```assembly
mov ax, 2000H    ;1
mov ds, ax       ;2
mov bx, 1000H    ;3
mov si, 0        ;4
mov ax, [bx+si]  ;5
inc si           ;6
mov cx, [bx+si]  ;7
inc si           ;8
mov di, si       ;9
mov ax, [bx+di]  ;10
```

- 执行1,2,3,4行代码后

| ax    | bx    | cx   | ds    | si   | di   |
| ----- | ----- | ---- | ----- | ---- | ---- |
| 2000H | 1000H |      | 2000H | 0    |      |

- 执行5，6行代码后

| ax    | bx    | cx   | ds    | si   | di   |
| ----- | ----- | ---- | ----- | ---- | ---- |
| 00BEH | 1000H |      | 2000H | 1    |      |

- 执行7，8行代码后

| ax    | bx    | cx    | ds    | si   | di   |
| ----- | ----- | ----- | ----- | ---- | ---- |
| 00BEH | 1000H | 0600H | 2000H | 2    |      |

- 执行9，10行代码后

| ax    | bx    | cx    | ds    | si   | di   |
| ----- | ----- | ----- | ----- | ---- | ---- |
| 0006H | 1000H | 0600H | 2000H | 2    | 2    |

## [bx+si+idata]和[bx+di+idata]方式寻址

### [bx+si+idata]和[bx+di+idata]方式指定地址

- [bx+si+idata]表示一个内存单元：偏移地址为(bx) + (si) + idata,即bx中的数值加上si中数值再加上idata
- 指令mov ax, [bx+si+idata]的含义
  - 将一个内存单元的内容送入ax
  - 这个内存单元的长度为2字节（字单元），存放一个字
  - 偏移地址为bx中的数值加上si中的数值再加上idata, 段地址在ds中
- 数学化的描述为(ax) = ((ds) * 16 + (bx) + (si) + idata)
- 指令mov ax, [bx+si+idata]的其他写法
  - `mov ax,  [bx+200+si]`
  - `mov ax, [200+bx+si]`
  - `mov ax, 200[bx][si]`
  - `mov ax, [bx].200[si]`
  - `mov ax, [bx][si].200`

### 应用案例

- 内存中数据 2000:1000 BE 00 06 00 6A 22
- 分析程序执行后，ax, bx, cx中的内容

```assembly
mov ax, 2000H      ;1
mov ds, ax         ;2
mov bx, 1000H      ;3
mov si, 0          ;4
mov ax, [bx+si+2]  ;5
inc si             ;6
mov cx, [bx+si+2]  ;7
inc si             ;8
mov di, si         ;9
mov ax, [bx+di+2]  ;10
```

- 执行1,2,3,4行代码后

| ax    | bx    | cx   | ds    | si   | di   |
| ----- | ----- | ---- | ----- | ---- | ---- |
| 2000H | 1000H |      | 2000H | 0    |      |

- 执行5，6行代码后

| ax    | bx    | cx   | ds    | si   | di   |
| ----- | ----- | ---- | ----- | ---- | ---- |
| 0006H | 1000H |      | 2000H | 1    |      |

- 执行7，8行代码后

| ax    | bx    | cx    | ds    | si   | di   |
| ----- | ----- | ----- | ----- | ---- | ---- |
| 0006H | 1000H | 6A00H | 2000H | 2    |      |

- 执行9，10行代码后

| ax    | bx    | cx    | ds    | si   | di   |
| ----- | ----- | ----- | ----- | ---- | ---- |
| 226AH | 1000H | 0600H | 2000H | 2    | 2    |

## 不同的寻址方式的灵活应用

### 对内存的寻址方式

| 形式          | 名称             | 特点                         | 意义                                               | 示例                            |
| ------------- | ---------------- | ---------------------------- | -------------------------------------------------- | ------------------------------- |
| [idata]       | 直接寻址         | 用一个常量、立即数来表示地址 | 用于直接定位一个内存单元                           | mov ax, [200]                   |
| [bx]          | 寄存器间接寻址   | 用一个变量来表示内存地址     | 用于间接定位一个内存单元                           | mov bx, 0<br />mov ax, [bx]     |
| [bx+idata]    | 寄存器相对寻址   | 用一个变量和常量表示地址     | 可在一个起始地址的基础上用变量间接定位一个内存单元 | mov bx, 4<br />mov ax, [bx+200] |
| [bx+si]       | 基址变址寻址     | 用两个变量表示地址           |                                                    | mov ax, [bx+si]                 |
| [bx+si+idata] | 相对基址变址寻址 | 用两个变量和一个常量表示地址 |                                                    | mov ax, [bx+si+200]             |

### 案例一

- 编程将datasg段中每个单词的头一个字母改为大写字母

```assembly
assume: cs:codesg, ds:datasg
datasg segment
	db '1. file         '
	db '2. edit         '
	db '3. search       '
	db '4. view         '
	db '5. options      '
	db '6. help         '
datasg ends
codesg segment
start: mov ax, datasg
			 mov ds, ax
			 mov bx, 0
			 mov cx, 6
		s: mov al, [bx+3]
		   and al, 11011111B
		   mov [bx+3], al
		   add bx, 16
		   loop s

			 mov ax, 4C00H
			 int 21H
codesg ends
end start
```

### 案例二

- 编程将datasg段中每个单词改为大写字母

- 4个字符串，看成一个4行16列的二维数组
- 要修改二维数组的每一行的前3列
- 构造4*3的二重循环

```assembly
assume cs:codesg, ds:datasg
datasg segment
	db 'ibm             '
	db 'dec             '
	db 'dos             '
	db 'vax             '
datasg ends
codesg segment
start: mov ax, datasg
	     mov ds, ax
	     mov bx, 0
	     mov cx, 4  ;循环次数
	 s0: mov si, 0
	 	   mov cx, 3  ;循环次数由cx定，可是cx只有一个。代码有bug
	 	s: mov al, [bx+si]
	 	   and al, 11011111B
	 	   mov [bx+si], al
	 	   inc si
	 	   loop s
	 	   add bx, 16
	 	   loop s0
			 
			 mov ax, 4C00H
			 int 21H
codesg ends
end start
```

- 方法一：用dx保存外存循环次数

```assembly
codesg segment
start: mov ax, datasg
	     mov ds, ax
	     mov bx, 0
	     mov cx, 4
	 s0: mov dx, cx ; 将外层循环的cx值保存在dx中
	     mov si, 0
	     mov cx, 3
	  s: mov al, [bx+si]
	     and al, 11011111B
	     mov [bx+si], al
	     inc si
	     loop s
	     add bx, 16
	     mov cx, dx ; 用dx中存放的外层循环的计数值恢复cx
	     loop s0    ; (cx) = (cx) - 1针对外层循环
codesg ends
end start
```

- 方法二：用固定的内存空间保存数据

```assembly
codesg segment
start: mov ax, datasg
	     mov ds, ax
	     mov bx, 0
	     mov cx, 4
	 s0: mov ds:[40H], cx ; 将外层循环的cx值保存在ds:[40H]中
	     mov si, 0
	     mov cx, 3
	  s: mov al, [bx+si]
	     and al, 11011111B
	     mov [bx+si], al
	     inc si
	     loop s
	     add bx, 16
	     mov cx, ds:[40H] ; 用ds:[40H]中存放的外层循环的计数值恢复cx
	     loop s0    ; (cx) = (cx) - 1针对外层循环
codesg ends
end start
```

- 方法三：用栈保存数据

```assembly
assume cs:codesg, ds:datasg, ss:stacksg
datasg segment
	db 'ibm             '
	db 'dec             '
	db 'dos             '
	db 'vax             '
datasg ends
stacksg segment
	dw 0,0,0,0,0,0,0,0
stacksg ends
codesg segment
start: mov ax, stacksg
			 mov ss, ax
			 mov sp 16
       mov ax, datasg
	     mov ds, ax
	     mov bx, 0
	     mov cx, 4  ;循环次数
	 s0: push cx    ;将外层循环的cx值压栈
	     mov si, 0
	 	   mov cx, 3  ;cx设置为内存循环的次数
	 	s: mov al, [bx+si]
	 	   and al, 11011111B
	 	   mov [bx+si], al
	 	   inc si
	 	   loop s
	 	   add bx, 16
	 	   pop cx ;从栈顶弹出原cx的值，恢复cx
	 	   loop s0
			 
			 mov ax, 4C00H
			 int 21H
codesg ends
end start
```

## 不同寻址方式总结

| 寻址方式                                                     | 含义                                                         | 名称             | 常用格式举例                                                 |
| ------------------------------------------------------------ | ------------------------------------------------------------ | ---------------- | ------------------------------------------------------------ |
| [idata]                                                      | EA=idata; SA=(ds)                                            | 直接寻址         | [idata]                                                      |
| [bx]<br />[si]<br />[di]<br />[bp]                           | EA=(bx); SA=(ds)<br />EA=(si); SA=(ds)<br />EA=(di); SA=(ds)<br />EA=(bp); SA=(ss) | 寄存器间接寻址   | [bx]                                                         |
| [bx+idata]<br />[si+idata]<br />[di+idata]<br />[bp+idata]   | EA=(bx)+idata; SA=(ds)<br />EA=(si)+idata; SA=(ds)<br />EA=(di)+idata; SA=(ds)<br />EA=(bp)+idata; SA=(ss) | 寄存器相对寻址   | 常用于结构体: bx.idata<br />用于数组: idata[si], idata[di]<br />用于二维数组: `[bx][idata]` |
| [bx+si]<br />[bx+di]<br />[bp+si]<br />[bp+di]               | EA=(bx)+(si); SA=(ds)<br />EA=(bx)+(di); SA=(ds)<br />EA=(bp)+(si); SA=(ss)<br />EA=(bp)+(di); SA=(ss) | 基址变址寻址     | 用于二维数组: `[bx][si]`                                     |
| [bx+si+idata]<br />[bx+di+idata]<br />[bp+si+idata]<br />[bp+di+idata] | EA=(bx)+(si)+idata; SA=(ds)<br />EA=(bx)+(di)+idata; SA=(ds)<br />EA=(bp)+(si)+idata; SA=(ss)<br />EA=(bp)+(di)+idata; SA=(ss) | 相对基址变址寻址 | 用于表格（结构体）中的数组项，[bx].idata[si]<br />用于二维数组：`idata[bx][si]` |

## 用于内存寻址的寄存器

- 只有bx, bp, si, di可以用在[...]对内存单元寻址

```assembly
mov ax, [bx]
mov ax, [bx+si]
mov ax, [bx+di]
mov ax, [bp]
mov ax, [bp+si]
mov ax, [bp+di]
```

- bx以外的通用寄存器，段寄存器不可以用在[...]中

```assembly
; 以下指令都是错误的
mov ax, [cx]
mov ax, [ax]
mov ax, [dx]
mov ax, [ds]
```

- bx, bp的区别：
  - bx默认指ds段
  - bp默认指ss段

```assembly
mov ax, [bp]           ; (ax) = ((ss) * 16 + (bp))
mov ax, ds:[bp]        ; (ax) = ((ds) * 16 + (bp))
mov ax, es:[bp]        ; (ax) = ((es) * 16 + (bp))
mov ax, [bx]           ; (ax) = ((ds) * 16 + (bx))
mov ax, ss:[bx]        ; (ax) = ((ss) * 16 + (bx))
mov ax, [bp+idata]     ; (ax) = ((ss) * 16 + (bp) + idata)
mov ax, [bp+si]        ; (ax) = ((ss) * 16 + (bp) + (si))
mov ax, [bp+si+idata]  ; (ax) = ((ss) * 16 + (bp) + (si) + idata)
; 下面的指令都是是错误
mov ax, [bx+bp] ;error
mov ax, [si+di] ;error
```

## 在哪里？有多长？

### 两个基本问题

- 处理的数据在什么地方
- 要处理的数据有多长

### 汇编语言中数据位置的表达

- 立即数(idata): 对于直接包含在机器指令中的数据，称为立即数(idata), 数据包含在指令中

```assembly
mov ax, 1
add bx, 2000H
or bx, 00010000B
mov al, 'a'
```

- 寄存器: 指令要处理的数据在寄存器中，在汇编指令中给出相应的寄存器名

```assembly
mov ax, bx
mov ds, ax
push bx
mov ds:[0], bx
push ds
mov ss, ax
mov sp, ax
```

- 内存：段地址(SA)和偏移地址(EA): 指令要处理的数据在内存中，由SA:EA确定内存单元

```assembly
; 段地址默认在ds中
mov ax, [0]
mov ax, [di]
mov ax, [bx+8]
mov ax, [bx+si]
mov ax, [bx+si+8]
; 段地址默认在ss中
mov ax, [bp]
mov ax, [bp+8]
mov ax, [bp+si]
mov ax, [bp+si+8]
; 显性的给出存放段地址的寄存器
mov ax, ds:[bp]       ; (ax)=((ds)*16+(bp))
mov ax, es:[bx]       ; (ax)=((es)*16+(bx))
mov ax, ss:[bx+si]    ; (ax)=((ss)*16+(bx)+(si))
mov ax, cs:[bx+si+8]  ; (ax)=((cs)*16+(bx)+(si)+8)
```

### 指令要处理的数据有多长

- 字word操作

```assembly
mov ax, 1
mov bx, ds:[0]
mov ds, ax
mov ds:[0] ax
inc ax
add ax, 1000H
```

- 字节byte 操作

```assembly
mov al, 1
mov al, bl
mov al, ds:[0]
mov ds:[0], al
inc al
add al, 100H
```

- 用word ptr或byte ptr指明: 在没有寄存器参与的内存单元访问指令中，用word ptr或byte ptr显性地指明所要访问的内存单元长度是很必要的，否则，CPU无法得知所要访问的单元是字单元还是字节单元

```assembly
mov word ptr ds:[0], 1
inc word ptr [bx]
inc word ptr ds:[0]
add word ptr [bx], 2

mov byte ptr ds:[0], 1
inc byte ptr [bx]
inc byte ptr ds:[0]
add byte ptr [bx], 2
```

## 寻址方式的综合应用

### 应用问题

- 关于姚明2001年的一条记录
  - 姓名： Yao
  - 生日：'19800912'
  - 球衣号码：15
  - 场均得分：32
  - 效力球队：SHH(上海)
- 2002年，姚明的信息有了变化
  - 球衣号码变成了11号
  - 场均得分为13
  - 效力球队变为NBA的休斯顿火箭队(HOU)
- 任务：编程修改内存中的过时数据

### 解决方案

| 内存偏移      | 2001年数据 | 2002年数据 |
| ------------- | ---------- | ---------- |
| seg:60    +00 | 'Yao'      | 'Yao'      |
| +03           | '19809912' | '19800912' |
| +0C           | 15         | 11         |
| +0E           | 32         | 13         |
| +10           | 'SHH'      | 'HOU'      |

```assembly
mov ax, seg
mov ds, ax
mov bx, 60H
mov word ptr [bx+0CH], 11
mov word ptr [bx+0EH], 13

mov si, 0
mov byte ptr [bx+10H+si], 'H'
inc si
mov byte ptr [bx+10H+si], 'O'
inc si
mov byte ptr [bx+10H+si], 'U'
```

### C语言和汇编的处理方式对比

```c
#include <stdio.h>
struct Player {
  char name[3];
  char birthday[9];
  int num;
  int ppg; // points per game
  char team[3];
};
struct Player yao = {"Yao", "19800912", 15, 32, "SHH"};
int main () {
  int i;
  yao.num = 11;
  yao.ppg = 13;
  i = 0;
  yao.team[i] = 'H';
  i++;
  yao.team[i] = 'O';
  i++;
  yao.team[i] = 'U';s
  return 0;
}
```

- yao.team[i]: yao是一个变量名，指明了结构体变量的地址； team是一个名称，指明了数据项team的地址；i用来定位team中的字符
- 用bx定位整个结构体；用idadta定位结构体中的某一个数据项；用si定位数据项中的元素
- 8086CPU提供的如[bx+si+idata]的寻址方式为结构体数据的处理提供了方便，使得我们可以在编程的时候，从结构化的角度去看待所要处理的数据。

## 用div指令实现除法

### div指令

- div是除法指令，使用div作除法的时候
  - 被除数：（默认）放在ax或dx和ax中
  - 除数：8为或16位，在寄存器或内存单元中
- div指令格式
  - div 寄存器
  - div 内存单元
- 切记提前在默认的寄存器中设置好被除数，且默认寄存器不作别的用处

| 被除数 | ax              | dx和ax           |
| ------ | --------------- | ---------------- |
| 除数   | 8位内存或寄存器 | 16位内存或寄存器 |
| 商     | al              | ax               |
| 余数   | ah              | dx               |

| 示例指令               | 被除数           | 除数                  | 商   | 余数 |
| ---------------------- | ---------------- | --------------------- | ---- | ---- |
| div bl                 | (ax)             | (bl)                  | (al) | (ah) |
| div byte ptr ds:[0]    | (ax)             | ((ds)*16+0)           | (al) | (ah) |
| div byte ptr [bx+si+8] | (ax)             | ((ds)*16+(bx)+(si)+8) | (al) | (ah) |
| div bx                 | (dx)*10000H+(ax) | (bx)                  | (ax) | (dx) |
| div word ptr es:[0]    | (dx)*10000H+(ax) | ((es)*16+0)           | (ax) | (dx) |
| div word ptr [bx+si+8] | (dx)*10000H+(ax) | ((dx)*16+(bx)+(si)+8) | (ax) | (dx) |

### div指令示例

1. 编程：利用除法指令计算100001/100

- 分析
  - 100001D=186A1H
  - 需要进行16位的除法
  - 用dx和ax两个寄存器联合存放186A1H
  - 用bx存放除数100D=64H

```assembly
mov dx, 1
mov ax, 86A1H
mov bx, 64H
div bx
```

2. 编程：利用除法指令计算1001/100

- 分析
  - 进行8位除法即可
  - 在ax寄存器存放被除数3E9H
  - 用bx存放除数100D=64H

```assembly
mov ax, 03E9H
mov bl, 64H
div bl
```

### 在内存单元中实施除法

- 双字型数据的定义
  - db 1 ;定义字节型数据01H, 在data: 0处，占1个字节
  - dw 1 ;定义字型数据0001H, 在data:1处，占2个字节
  - dd 1 ; 定义双字型数据0000 0001H，在data: 3处，占2个字（4个字节）

- 用div计算data段中第一个数据除以第二个数据后的结果，商存在第三个数据的存储单元中

```assembly
data segment
	dd 100001
	dw 100
	dw 0
data ends
code segment
	mov ax, data
	mov ds, ax
	mov ax, ds:[0]
	mov dx, ds:[2]
	div word ptr ds:[4]
	mov ds:[6], ax
code ends
```

## 用dup设置内存空间

### dup功能和用法

- 功能：dup和db, dw, dd等数据定义伪指令配合使用，用来进行数据的重复
- 示例

| 指令                    | 功能                                     | 相当于                  |
| ----------------------- | ---------------------------------------- | ----------------------- |
| db 3 dup(0)             | 定了3个字节，它们的值都是0               | db 0,0,0                |
| db 3 dup(0,1,2)         | 定义了9个字节，由0，1，2重复3次构成      | db 0,1,2,0,1,2,0,1,2    |
| db 3 dup ('abc', 'ABC') | 定义了18个字节，构成'abcABCabcABCabcABC' | db 'abcABCabcABCabcABC' |

- dup的使用格式
  - db重复的次数dup （重复的字节型数据）
  - dw重复的次数dup （重复的字型数据）
  - dd重复的次数dup （重复的双字数据）

### dup用途

- 定义一个容量为20个字节的栈段

  - 不采用dup的格式

  ```assembly
  stack segment
  	dw 0,0,0,0,0,0,0,0,0,0
  stack ends
  ```

  - 采用dup的格式

  ```assembly
  stack segment
  	db 20 dup (0)
  stack ends
  ```
